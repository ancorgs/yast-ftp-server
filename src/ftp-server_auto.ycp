
/**
 * File:	clients/ftp-server_auto.ycp
 * Package:	Configuration of ftp-server
 * Summary:	Client for autoinstallation
 * Authors:	Jozef Uhliarik <juhliarik@suse.cz>
 *
 * $Id: ftp-server_auto.ycp 27914 2006-02-13 14:32:08Z locilka $
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param function to execute
 * @param map/list of ftp-server settings
 * @return map edited settings, Summary or boolean on success depending on called function
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = WFM::CallFunction ("ftp-server_auto", [ "Summary", mm ]);
 */

{

textdomain "ftp-server";

y2milestone("----------------------------------------");
y2milestone("FtpServer auto started");

import "FtpServer";
include "ftp-server/wizards.ycp";

any ret = nil;
string func = "";
map param = $[];

/* Check arguments */
if(size(WFM::Args()) > 0 && is(WFM::Args(0), string)) {
    func = (string)WFM::Args(0);
    if(size(WFM::Args()) > 1 && is(WFM::Args(1), map))
	param = (map) WFM::Args(1);
}
y2debug("func=%1", func);
y2debug("param=%1", param);

/* Create a summary*/
if(func == "Summary") {
    ret = FtpServer::Summary();
    //ret = select(FtpServer::Summary(), 0, "");
}
/* Reset configuration */
else if (func == "Reset") {
    FtpServer::Import($[]);
    ret = $[];
}
/* Change configuration (run AutoSequence) */
else if (func == "Change") {
    ret = FtpServerAutoSequence();
}
/* Import configuration */
else if (func == "Import") {
    ret = FtpServer::Import(param);
}
/* Return actual state */
else if (func == "Export") {
    ret = FtpServer::Export();
}
/* Return needed packages */
else if (func == "Packages") {
    ret = FtpServer::AutoPackages();
}

/**
 * Return if configuration  was changed
 * return boolean
 */
else if (func == "GetModified") {
    ret = FtpServer::modified;
}
/**
 * Set modified flag
 * return boolean
 */
else if (func == "SetModified") {
    FtpServer::modified = true;
    ret = true;
}

/* Read current state */
else if (func == "Read") {
    import "Progress";
    boolean progress_orig = Progress::set (false);
    FtpServer::InitDaemon ();
    ret = FtpServer::Read();
    Progress::set (progress_orig);
}
/* Write givven settings */
else if (func == "Write") {
    import "Progress";
    boolean progress_orig = Progress::set (false);
    FtpServer::write_only = true;
    ret = FtpServer::Write();
    Progress::set (progress_orig);
}
/* Unknown function */
else {
    y2error("Unknown function: %1", func);
    ret = false;
}

y2debug("ret=%1", ret);
y2milestone("FtpServer auto finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
