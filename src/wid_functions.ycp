/**
 * File:	include/ftpd/wid_functions.ycp
 * Package:	Configuration of ftpd
 * Summary:	Wizards definitions
 * Authors:	Jozef Uhliarik <juhliarik@suse.cz>
 *
 * $Id: wid_functions.ycp 27914 2006-02-13 14:32:08Z juhliarik $
 */

{

import "Popup";
import "Wizard";
import "Report";
import "Label";
import "Inetd";
import "Service";
import "Mode";
import "Users";


include "ftp-server/uiforms.ycp";

/**
 * CWMServiceStart function with no parameter returning boolean value 
 * that says if the service is started.
 */
global boolean GetEnableService () {
  boolean result = false;
  if (FtpServer::EDIT_SETTINGS["StartDaemon"]:nil == "1")
     result = true;
  else
     result = false;   
  return result;
}


/**
 * CWMServiceStart function with one boolean parameter  
 * returning boolean value that says if the service will be started at boot.
 */
global void SetEnableService (boolean enable_service) {  
  if (enable_service)
     FtpServer::EDIT_SETTINGS["StartDaemon"] = "1";
}

/**
 * CWMServiceStart function with no parameter returning boolean value 
 * that says if the service is started.
 */
global boolean GetStartedViaXinetd () {
  boolean result = false;
  if (FtpServer::EDIT_SETTINGS["StartDaemon"]:nil == "2")
     result = true;
  else
     result = false;     
  return result;
}


/**
 * CWMServiceStart function with one boolean parameter  
 * returning boolean value that says if the service will be started at boot.
 */
global void SetStartedViaXinetd (boolean enable_service) {
  boolean result = true;
  if ((Service::Status("xinetd") != 0) && (FtpServer::EDIT_SETTINGS["StartXinetd"]:nil == "NO") 
     && (enable_service)) {
     //Popup::Message("xinetd is not started");
     if (Mode::normal()) {
        UI::OpenDialog(       
          `VBox(
	    `Label("Xinetd is not started!"),
	    `Label("Do you want to start it?"),
	    `HBox(
	      `PushButton(`id(`accept), "&Start"),
	      `PushButton(`id(`cancel), "Canc&el")
            )
          ) // end of `VBox(
        ); // end of UI::OpenDialog(
        while (true) {
          any ret = UI::UserInput();
          if ( ret == `accept ) {
             //FtpServer::EDIT_SETTINGS["StartXinetd"] = "YES";
             result = true;
	     break;
          } else if (ret == `cancel) {
             //FtpServer::EDIT_SETTINGS["StartXinetd"] = "NO";
             result = false;
	     break;
          }
        };
        UI::CloseDialog();
     } 
  } //end of if (Service::Status("xinetd") != 0)
  if (enable_service) {
     FtpServer::EDIT_SETTINGS["StartDaemon"] = "2";
     if (result)
        FtpServer::EDIT_SETTINGS["StartXinetd"] = "YES";
     else
        FtpServer::EDIT_SETTINGS["StartXinetd"] = "NO";
  } else { 
     FtpServer::EDIT_SETTINGS["StartDaemon"] = "0";
     FtpServer::EDIT_SETTINGS["StartXinetd"] = "NO";
  }

}
/**
 * Init function for general settings
 * change ValidChars for textentry
 */
void InitGenSettings(string key) {
  if (!FtpServer::vsftpd_edit)  {
     UI::ChangeWidget(`id( "Umask" ), `ValidChars, "01234567:");   
     UI::ChangeWidget(`id( "Umask" ), `Value,  FtpServer::ValueUIEdit("Umask"));	
  } else {     
     UI::ChangeWidget(`id( "UmaskAnon" ), `ValidChars, "01234567");
     UI::ChangeWidget(`id( "UmaskLocal" ),`ValidChars, "01234567");
     UI::ChangeWidget(`id("Banner"), `Value, FtpServer::ValueUIEdit("Banner"));
     UI::ChangeWidget(`id( "UmaskAnon" ), `Value,  FtpServer::ValueUIEdit("UmaskAnon"));
     UI::ChangeWidget(`id( "UmaskLocal" ), `Value,  FtpServer::ValueUIEdit("UmaskLocal"));
     UI::ChangeWidget(`id( "FtpDirLocal" ), `Value, FtpServer::ValueUIEdit("FtpDirLocal"));
     
  }  
  UI::ChangeWidget(`id( "FtpDirAnon" ), `Value, FtpServer::ValueUIEdit("FtpDirAnon"));
  UI::ChangeWidget(`id( "ChrootEnable" ), `Value, (FtpServer::ValueUIEdit("ChrootEnable") == "YES"));	
  UI::ChangeWidget(`id( "VerboseLogging" ), `Value, (FtpServer::ValueUIEdit("VerboseLogging") == "YES"));
  
}


/**
 * Handle function for general settings
 * handling started via Xinetd
 */

symbol HandleGenSettings(string key, map event) {
   any button = event["ID"]:nil;
   if (FtpServer::vsftpd_edit)  {      
      if (button == "SelectDirLocal") {
          string val = UI::AskForExistingDirectory  ("/","Please select directory...");
          UI::ChangeWidget(`id( "FtpDirLocal" ), `Value, val);
      }
   } // end of if (FtpServer::vsftpd_edit)
   if (button == "SelectDirAnon") {
      string val = UI::AskForExistingDirectory  ("/","Please select directory...");
      UI::ChangeWidget(`id( "FtpDirAnon" ), `Value, val);
   } 
} // end of symbol HandleGenSettings(string key, map event) {

/**
 * Validate function for general settings
 * checks values from textentry
 */
boolean GenValid (string key, map event) {

  if (!FtpServer::vsftpd_edit) {
     string new_umask = (string)UI::QueryWidget (`id("Umask"), `Value);
  
     if (size(new_umask) > 0) {
         list <string> l = splitstring (new_umask, ":");
         l = filter (string s, l, ``(s != ""));
         if (size(l) < 2) {
      	     Popup::Message ("It is not valid umask!");
	     UI::SetFocus(`id("Umask"));
             return false;
         }
      
     }
     string AnonHomeDir = (string)UI::QueryWidget (`id("FtpDirAnon"), `Value);
     //checking correct homedir for anonymous user
     if ((AnonHomeDir != nil) && (AnonHomeDir != "")) {
        
        if (AnonHomeDir != FtpServer::anon_homedir) {
           string error = Users::EditUser($["homedirectory":AnonHomeDir]);
           if ((error != nil) && (error != "")) {
              Popup::Error(error);
	      UI::SetFocus(`id("FtpDirAnon"));
	      return false;
	   }
           integer uid = FtpServer::anon_uid;
           boolean failed = false;
	   map<string,integer> ui_map = $[];
           map error_map = $[];
	   do {
	     error_map = Users::CheckHomeUI (uid, AnonHomeDir, ui_map);
	     if (error_map != $[]) {
		if (!Popup::YesNo (error_map ["question"]:""))
		   failed = true;
		else
		   ui_map[ error_map["question_id"]:"" ] = 1;
	     }
	   } while (error_map != $[] && !failed);
           /*
           error = Users::CheckHomeUI();
           if ((error != nil) && (error != "")) {
              Popup::Error(error);
	      UI::SetFocus(`id("FtpDirAnon"));
	      return false;
	   }*/
	}   
     }
  }  
  return true;
}

/**
 * Store function for general settings
 * save values to temporary structure
 */

void StoreGenSettings (string key, map event) {  
  foreach(string key, ["ChrootEnable", "VerboseLogging"],{
     FtpServer::WriteToEditMap(key, (((boolean) UI::QueryWidget(`id(key), `Value) == true) ? "YES":"NO"));
  });
  FtpServer::WriteToEditMap("FtpDirAnon", (string) UI::QueryWidget(`id("FtpDirAnon"), `Value));
  if (!FtpServer::vsftpd_edit) {
     FtpServer::WriteToEditMap("Umask", (string) UI::QueryWidget(`id("Umask"), `Value));
  } else {
     foreach(string key, ["UmaskAnon", "UmaskLocal", "FtpDirLocal", "Banner"],{
        FtpServer::WriteToEditMap(key, (string) UI::QueryWidget(`id(key), `Value));
     });

  }

}

/**
 * Init function for general settings
 * save values to temporary structure
 */  
void InitRBVsPure (string key) {
  if ((FtpServer::vsftpd_installed) && (FtpServer::pureftpd_installed)) {
     if (FtpServer::vsftpd_edit) {
        UI::ChangeWidget(`id( "vs_item" ), `Value, true);
     }else
        UI::ChangeWidget(`id( "pure_item" ), `Value, true);
  } else UI::ChangeWidget(`id("items"), `opt(`disabled), true);
}



symbol HandleRBVsPure(string key, map event) {
  if ((FtpServer::vsftpd_edit) && ((boolean) UI::QueryWidget (`id("pure_item"), `Value)) 
     && FtpServer::vsftpd_installed) {
     FtpServer::vsftpd_edit = false;
     return `pureftpd;
  }
  if ((!FtpServer::vsftpd_edit) && ((boolean) UI::QueryWidget (`id("vs_item"), `Value)) 
     && FtpServer::pureftpd_installed) {
     FtpServer::vsftpd_edit = true;
     return `vsftpd;
  }

}
/**
 * Init function for performance settings
 * change ValidChars from textentry
 */
void InitPerSettings (string key) {

  foreach(string key, ["MaxIdleTime", "MaxClientsPerIP", "MaxClientsNumber", 
		       "LocalMaxRate", "AnonMaxRate"],{
      //UI::ChangeWidget(`id(key), `ValidChars, "0123456789");
      UI::ChangeWidget(`id(key), `Value, tointeger(FtpServer::ValueUIEdit(key)));
  });
}

/**
 * Store function for performance settings
 * save values to temporary structure
 */
void StorePerforSettings (string key, map event) {
  foreach(string key, ["MaxIdleTime", "MaxClientsPerIP", "MaxClientsNumber", 
		       "LocalMaxRate", "AnonMaxRate"],{
      FtpServer::WriteToEditMap(key, tostring(UI::QueryWidget(`id(key), `Value)));
  });
}

/**
 * Init function for performance settings
 * change ValidChars from textentry
 */
void InitAnonSettings(string key) {
  integer current = (integer) UI::QueryWidget(`id("AnonAuthen"), `CurrentButton);
  integer val = tointeger(FtpServer::ValueUIEdit("AnonAuthen"));
  UI::ChangeWidget(`id("AnonAuthen"), `CurrentButton, val);
  UI::ChangeWidget(`id( "AnonReadOnly" ), `Value, 
      (FtpServer::ValueUIEdit("AnonReadOnly") == "NO"));
  UI::ChangeWidget(`id( "AnonCreatDirs" ), `Value, 
      (FtpServer::ValueUIEdit("AnonCreatDirs") == "YES"));


}


/**
 * Store function for performance settings
 * save values to temporary structure
 */
void StoreAnonSettings (string key, map event) {
  FtpServer::WriteToEditMap("AnonAuthen", tostring(UI::QueryWidget(`id("AnonAuthen"), `CurrentButton)));
  foreach(string key, ["AnonCreatDirs"],{
     FtpServer::WriteToEditMap(key, (((boolean) UI::QueryWidget(`id(key), `Value) == true) ? "YES":"NO"));
  });

  FtpServer::WriteToEditMap("AnonReadOnly", (((boolean) UI::QueryWidget(`id("AnonReadOnly"), `Value) == true) ? "NO":"YES"));
  
}



/**
 * Init function for pure-ftpd add settings
 * change values a display correct widget
 */
void InitPUREFTPTAddSettings (string key) {
  UI::ChangeWidget(`id( "AntiWarez" ), `Value, 
     (FtpServer::ValueUIEdit("AntiWarez") == "YES"));
  integer current = (integer) UI::QueryWidget(`id("SSL"), `CurrentButton);
  UI::ChangeWidget(`id("SSL"), `CurrentButton, tointeger(FtpServer::ValueUIEdit("SSL")));

}

/**
 * Store function for pure-ftpd add settings
 * save values to temporary structure
 */
void StorePUREFTPDSettings (string key, map event) {
  FtpServer::WriteToEditMap("AntiWarez", (((boolean) UI::QueryWidget(`id("AntiWarez"), `Value) == true) ? "YES":"NO"));
  FtpServer::WriteToEditMap("SSL", tostring(UI::QueryWidget(`id("SSL"), `CurrentButton)));

  //y2milestone("-------------PURE_EDIT_SETTINGS-------------------");  
  //y2milestone("Pure-ftpd configuration has been read: %1", FtpServer::PURE_EDIT_SETTINGS);
  //y2milestone("---------------------------------------------");
}


/**
 * Init function for vsftpd add settings
 * change values a display correct widget
 */
void InitVSFTPAddSettings (string key) {
  UI::ChangeWidget(`id( "SSLEnable" ), `Value, (FtpServer::ValueUIEdit("SSLEnable") == "YES"));
  UI::ChangeWidget(`id( "TLS" ), `Value, (FtpServer::ValueUIEdit("TLS") == "YES"));
  foreach(string key, ["PasMinPort", "PasMaxPort", "SSLVersion"],{
    UI::ChangeWidget(`id(key), `CurrentButton, tointeger(FtpServer::ValueUIEdit(key)));
  });
  
}

/**
 * Handle function for general settings
 * handling started via Xinetd
 */

 boolean ValidVSFTPAddPure(string key, map event) {
  integer min_port = tointeger(UI::QueryWidget (`id("PasMinPort"), `Value));
  integer max_port = tointeger(UI::QueryWidget (`id("PasMaxPort"), `Value));
    
  if (min_port > max_port) {
     Popup::Message ("Condition for ports is max port > min port.");
     UI::SetFocus(`id("PasMinPort"));
     return false;
  }
  return true;

}
/**
 * Store function for vsftpd add settings
 * save values to temporary structure
 */

void StoreVSFTPDSettings (string key, map event) {
  integer val = 4;
  string UIval = tostring(UI::QueryWidget(`id("SSLVersion"), `Value));
  if (UIval == "Enabled SSL v2")
     val = 0;
  else if (UIval == "Disabled SSL v2")
     val = 1;
  else if (UIval == "Enabled SSL v3")
     val = 2;
  else if (UIval == "Disabled SSL v3")
     val = 3;
  else if (UIval == "Enabled SSL v2 & v3")
     val = 4;
  else
     val = 5;
  FtpServer::WriteToEditMap("SSLVersion", tostring(val));
  FtpServer::WriteToEditMap("TLS", (((boolean) UI::QueryWidget(`id("TLS"), `Value) == true) ? "YES":"NO"));
  FtpServer::WriteToEditMap("SSLEnable", (((boolean) UI::QueryWidget(`id("SSLEnable"), `Value) == true) ? "YES":"NO"));
  //FtpServer::WriteToEditMap("Banner", (string) UI::QueryWidget(`id("Banner"), `Value));
  foreach(string key, ["PasMinPort", "PasMaxPort"],{
     FtpServer::WriteToEditMap(key, tostring(UI::QueryWidget(`id(key), `Value)));
  });
  y2milestone("-------------EDIT_SETTINGS-------------------");  
  y2milestone("vsftpd configuration has been read: %1", FtpServer::EDIT_SETTINGS);
  y2milestone("---------------------------------------------");
  
}


}